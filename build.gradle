apply plugin: "java"

ext {
    //general things
    javaVersion = "1.8"
    worldremapperVersion = "1.5.0"

    //dependency things
    gsonVersion = "2.8.5"
    junitVersion = "4.12"
    lombokVersion = "1.16.20"
    porklibVersion = "0.5.0-SNAPSHOT"
}

group "net.daporkchop"
version "$worldremapperVersion"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    mavenLocal()
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    mavenCentral()
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "net.daporkchop.lib:logging:$porklibVersion"
    shade "net.daporkchop.lib:minecraft-worldscanner:$porklibVersion"

    testCompile "junit:junit:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

jar {
    from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }.unique()
    } {
        exclude "META-INF/**"
        exclude sourceSets.main.output.asFileTree.collect {
            return it.getPath().split("java/main/").last()
        }
    }

    manifest {
        attributes "Implementation-Title": "DaPorkchop_",
                "Implementation-Version": "$worldremapperVersion",
                "Main-Class": "net.daporkchop.worldremapper.Main"
    }
}
